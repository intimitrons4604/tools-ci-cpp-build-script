apply plugin: 'cpp'

repositories {
    maven { url 'http://first.wpi.edu/FRC/roborio/maven/release' }
}

ext {
    /* Dependency versions */
    wpilibVersion = '2016.5.1'
}

configurations {
    wpilib
}

dependencies {
    wpilib "edu.wpi.first.wpilib.cmake:cpp-root:$wpilibVersion@zip"  
}

task extractWpilib(type: Sync) {
    from {
        configurations.wpilib.collect { zipTree(it) }
    }
    into "$buildDir/wpilib/"
}

model {
    buildTypes {
        debug
    }

    platforms {
        roboRio
    }

    toolChains {
        gcc(Gcc) {
            target ('roboRio') {
                // Use FRC provided toolchain
                def gccPrefix = 'arm-frc-linux-gnueabi-'
                cppCompiler.executable = gccPrefix + cppCompiler.executable
                linker.executable = gccPrefix + linker.executable
                assembler.executable = gccPrefix + assembler.executable

                cppCompiler.withArguments { args ->
                    args << '-std=c++1y' << '-Wformat=2' << '-Wall' << '-Wextra' << '-Werror' << '-pedantic'
                    args << '-Wno-psabi' << '-Wno-unused-parameter' << '-fPIC' << '-Og' << '-g3' << '-rdynamic'
                    args << '-Wno-error=deprecated-declarations'
                }

                linker.withArguments { args ->
                    args << '-rdynamic'
                }
            }
        }
    }

    repositories {
        lib(PrebuiltLibraries) {
            wpilib {
                headers.srcDir "$buildDir/wpilib/include"
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("$buildDir/wpilib/lib/libwpi.so")
                }
            }
        }
    }

    components {
        cppRobotProject(NativeExecutableSpec) {
            targetPlatform 'roboRio'

            binaries.all {
                tasks.withType(CppCompile) {
                    dependsOn extractWpilib
                }

                cppCompiler.args '-pthread', '-Wno-unused-variable'
                linker.args "-L$buildDir/wpilib/lib", '-pthread', '-Wno-unused-variable'
            }

            sources {
                cpp {
                    source {
                        srcDir 'src'
                        include '**/*.cpp'
                    }

                    lib library: 'wpilib', linkage: 'shared'
                }
            }
        }
    }
}

